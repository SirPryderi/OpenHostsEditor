plugins {
    id("com.github.triplet.play") version "2.8.0"
}

apply plugin: 'com.android.application'

android {
    signingConfigs {
        release
    }
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "me.vittorio_io.openhostseditor"
        minSdkVersion 15
        targetSdkVersion 29
        versionName '0.10.2-alpha'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables {
            useSupportLibrary true
        }
        versionCode 4
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    productFlavors {
    }

    def isRunningOnTravis = System.getenv("CI") == "true"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    if (isRunningOnTravis) {
        signingConfigs.release.storeFile = file("../upload-keystore.jks")
        signingConfigs.release.storePassword = System.getenv("keystore_password")
        signingConfigs.release.keyAlias = System.getenv("keystore_alias")
        signingConfigs.release.keyPassword = System.getenv("keystore_alias_password")
    }
}

play {
    serviceAccountCredentials = file("../service.json")
    promoteTrack = "alpha"
    resolutionStrategy = "auto"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.github.medyo:android-about-page:1.2.1'
    testImplementation 'junit:junit:4.12'
}

task printVersionName {
    doLast {
        println android.defaultConfig.versionName
    }
}
